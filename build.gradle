plugins {
  id 'java'
  id 'idea'
  alias libs.plugins.os.detector
  alias libs.plugins.spring.boot
  alias libs.plugins.spring.dependency.management
}

group = 'dev.lorentzen.branch'
version = '0.0.1-SNAPSHOT'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(23)
  }
}

repositories {
  mavenCentral()
}

springBoot {
  buildInfo()
}

sourceSets {
  integration {
    java {
      srcDirs = ['src/integration/java']
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
    }
    resources.srcDirs = main.resources.srcDirs + test.resources.srcDirs + ['src/integration/resources']
  }
}

idea {
  module {
    testSources.from(sourceSets.integration.java.srcDirs)
  }
}

configurations {
  integrationApi.extendsFrom testApi
  integrationCompile.extendsFrom testCompile
  integrationImplementation.extendsFrom testImplementation
  integrationRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
  implementation libs.apache.commons.lang
  implementation libs.bundles.spring.boot.starter
  implementation libs.caffeine
  implementation libs.springdoc.openapi.ui
  if (osdetector.arch == "aarch_64") {
     implementation "io.netty:netty-resolver-dns-native-macos:4.1.117.Final:osx-aarch_64"
  }
  testImplementation libs.spring.boot.starter.test
}

def integrationTest = tasks.register('integrationTest', Test) {
  group 'verification'
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

tasks.named('check') {
  dependsOn(integrationTest)
}

tasks.configureEach {
  if (it instanceof JavaForkOptions) {
    it.systemProperties System.properties
  }
}
